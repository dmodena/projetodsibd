-- Retorna o valor da opção pelo seu código
-- Recebe NUMBER Código da opção
-- Retorna NUMBER Valor da opção
CREATE OR REPLACE FUNCTION VALOR_OPCAO(
  I_COD_OPCAO OPCOES.CODIGO%TYPE
)
RETURN NUMBER IS
  CURSOR CUR_BI IS
    SELECT COD_BEBIDA, COD_ITEM, QUANTIDADE
    FROM BEBIDA_ITENS
    WHERE COD_BEBIDA = I_COD_OPCAO;
  CURSOR CUR_CI IS
    SELECT COD_COMIDA, COD_ITEM, QUANTIDADE
    FROM COMIDA_ITENS
    WHERE COD_COMIDA = I_COD_OPCAO;
  V_VALOR NUMBER(8,2) := 0;
  V_COUNT_OPCOES NUMBER;
  V_OPCAO_TIPO CHAR(1);
  V_PCT_LUCRO NUMBER;
  V_VALOR_ITEM NUMBER;
  OPCAO_INVALIDA EXCEPTION;
BEGIN
  SELECT COUNT(CODIGO) INTO V_COUNT_OPCOES
  FROM OPCOES
  WHERE CODIGO = I_COD_OPCAO;

  IF V_COUNT_OPCOES < 1 THEN
    RAISE OPCAO_INVALIDA;
  END IF;

  SELECT TIPO, PCT_LUCRO INTO V_OPCAO_TIPO, V_PCT_LUCRO
  FROM OPCOES
  WHERE CODIGO = I_COD_OPCAO;

  CASE V_OPCAO_TIPO
    WHEN 'B' THEN
      FOR R_CUR_BI IN CUR_BI LOOP
        SELECT VALOR * R_CUR_BI.QUANTIDADE INTO V_VALOR_ITEM
        FROM ITENS
        WHERE CODIGO = R_CUR_BI.COD_ITEM;
        V_VALOR := V_VALOR + V_VALOR_ITEM;
      END LOOP;
    WHEN 'C' THEN
      FOR R_CUR_CI IN CUR_CI LOOP
        SELECT VALOR * R_CUR_CI.QUANTIDADE INTO V_VALOR_ITEM
        FROM ITENS
        WHERE CODIGO = R_CUR_CI.COD_ITEM;
        V_VALOR := V_VALOR + V_VALOR_ITEM;
      END LOOP;
    ELSE
      SELECT VALOR INTO V_VALOR_ITEM
      FROM BEBIDAS_PRONTAS
      WHERE CODIGO = I_COD_OPCAO;
      V_VALOR := V_VALOR_ITEM;
  END CASE;

  V_VALOR := V_VALOR + (V_VALOR * V_PCT_LUCRO);

  RETURN V_VALOR;

  EXCEPTION
    WHEN OPCAO_INVALIDA THEN
      DBMS_OUTPUT.PUT_LINE('Código de opção inválido');
      RETURN NULL;
END;
/
